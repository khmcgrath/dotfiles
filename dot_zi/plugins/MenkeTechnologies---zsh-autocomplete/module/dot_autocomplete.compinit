#!/bin/zsh
zmodload -Fa zsh/files b:zf_rm
zmodload -F zsh/parameter p:funcstack p:functions

[[ -v _comp_setup ]] ||
  compdef() {
    unfunction compdef
    .autocomplete.compinit.precmd
    unfunction .autocomplete.compinit.precmd
    compdef "$@"
  }

_bash_complete compgen complete () {
  builtin autoload -Uz bashcompinit
  bashcompinit
  ${(%):-%N} "$@"
}

.autocomplete.compinit.precmd() {
  [[ -z $CDPATH ]] &&
    unset cdpath CDPATH

  local -i __ac_fd

  .autocomplete.compinit.zle-f() {
    exec {1}<&-
    zle -F $1 2>/dev/null
    unfunction ${(%):-%N}
  }
  zle -F "$__ac_fd" .autocomplete.compinit.zle-f

  builtin autoload -Uz .autocomplete.patch

  .autocomplete.patch _main_complete
  _main_complete() {
    local -i ret=1 _autocomplete__reserved_lines=0

    compstate[insert]=menu  # To get the best output from `_expand` (and some other functions).
    compstate[last_prompt]=yes  # Completion doesn't stay on same prompt without this.
    compstate[list]='list force packed rows' # `_setup` doesn't work well for this.

    local +h -a comppostfuncs=( _autocomplete._main_complete.post "$comppostfuncs[@]" )
    {
      # Don't break `_complete_help`, async completion, etc.
      [[ -v functions[compadd] ]] ||
        functions[compadd]=$functions[.autocomplete.compadd]

      .autocomplete._main_complete "$@"
      ret=$?

    } always {
      # Stop `_complete_help` from complaining.
      [[ $WIDGET == _complete_help ]] ||
        unfunction compadd
    }

    return ret
  }

  _autocomplete._main_complete.post() {
    compstate[list_max]=0
    MENUSCROLL=0
    if (( CURRENT > 1 && compstate[nmatches] > 1 )) && (
          ( [[ $_completer == expand ]] && _autocomplete.is_glob ) ||
          [[ $words[1] != cd &&
            $_comp_tags == *( (|-local)directories | (|(all|globbed)-)files ) ]]
        ); then
      _description all-matches expl 'all matches'
      compadd "$expl[@]" -C &&
        _comp_tags+=' all-matches'
    fi
    _autocomplete.unambiguous
  }

  .autocomplete.patch _expand
  _expand() {
    if _autocomplete.is_glob; then
      [[ $words[CURRENT] == *[^*] && $WIDGET != _complete_help ]] &&
        local ISUFFIX="*"

      .autocomplete._expand "$@"
      return
    fi

    .autocomplete._expand "$@" ||
      return

    (( compstate[nmatches] > 1 )) &&
      return 0

    local exp=${(Q)compstate[unambiguous]} word=$IPREFIX$PREFIX$SUFFIX$ISUFFIX

    [[ $exp == ($word|$compstate[unambiguous]) ]] &&
      return 0

    local expl
    _description -V expansions expl expansions "o:$word"
    compadd "$expl[@]" -UQ -qS '' - "${(q+)exp}"

    return 0
  }

  .autocomplete.patch _complete
  _complete() {
    .autocomplete._complete "$@"
    local -i ret=$?
    (( ret )) &&
      _comp_mesg='' # Otherwise, the next completer won't get tried.
    return ret
  }

  .autocomplete.patch _approximate
  _approximate() {
    [[ -z $words[CURRENT] || -v compstate[quote] ]] &&
      return 1
    [[ -o banghist && $words[CURRENT] == [$histchars]* ]] &&
      return 1
    _autocomplete.is_glob &&
      return 1

    local -i ret=1
    {
      [[ -v functions[compadd] ]] &&
        functions[.autocomplete.__tmp__]=$functions[compadd]

      compadd() {
        local ppre="$argv[(I)-p]"

        [[ ${argv[(I)-[a-zA-Z]#U[a-zA-Z]#]} -eq 0 &&
          "${#:-$PREFIX$SUFFIX}" -le _comp_correct ]] && return

        if [[ "$PREFIX" = \~* && ( ppre -eq 0 || "$argv[ppre+1]" != \~* ) ]]; then
          PREFIX="~(#a${_comp_correct})${PREFIX[2,-1]}"
        else
          PREFIX="(#a${_comp_correct})$PREFIX"
      fi

        builtin compadd "$@"
      }

      .autocomplete._approximate "$@"
      ret=$?

    } always {
      if [[ -v functions[.autocomplete.__tmp__] ]]; then
        functions[compadd]=$functions[.autocomplete.__tmp__]
        unfunction .autocomplete.__tmp__
      fi
    }
    return ret
  }

  .autocomplete.patch _wanted
  _wanted() {
    if [[ $funcstack == *_parameters* ]] &&
        zstyle -T ":completion:${curcontext}:parameters" verbose; then
      local -a params=( $@[(re)-,-1] )
      shift -p $#params
      shift params
      _description "$@[1,3]"
      builtin compadd "$expl[@]" "$@[5,-1]" -D params -a params
      local -a displays=()
      local sep; zstyle -s ":completion:${curcontext}:parameters" list-separator sep ||
        sep=--
      local MATCH MBEGIN MEND
      zformat -a displays " $sep " \
        "${(@)params[@]:/(#m)*/${MATCH}:${${(kv)${(P)MATCH}}[1,COLUMNS]}}"
      displays=( "${(@)displays[@]//(#m)[^[:print:]]##/${(q+)MATCH}}" )
      displays=( "${(@)displays[@]:/(#m)*/$MATCH[1,COLUMNS]}" )
      .autocomplete._wanted "$@" -d displays -a params
    else
      .autocomplete._wanted "$@"
    fi
  }

  .autocomplete.compadd() {
    setopt localoptions listtypes

    if [[ $_completer == expand* ]]; then
      builtin compadd -f -W "${${${words[CURRENT]:#[~/]*}:+$PWD/}:-/}" "$@"
    else
      builtin compadd "$@"
    fi
  }
}
